<S> T_EOF
T_FN T_IDENTIFIER T_I32 T_F32 T_CHAR T_BOOL T_ASSIGN T_MUT T_LEFT_PAREN T_RIGHT_PAREN T_LEFT_BRACE T_RIGHT_BRACE T_EOF T_SEMICOLON T_INTEGER_LITERAL T_STRING_LITERAL T_CHAR_LITERAL T_FLOAT_LITERAL T_RETURN T_COMMA T_COLON T_ARROW T_LET T_PLUS T_MINUS T_MULTIPLY T_DIVIDE T_LESS T_GREATER T_EQUAL T_GREATEREQUAL T_LESSEQUAL T_NOTEQUAL T_IF T_ELSE T_WHILE T_FOR T_LOOP T_IN T_DOTDOT T_BREAK T_CONTINUE T_REFERENCE

<S> ::= <Program>
<Program> ::= <declaration_list>
<declaration_list> ::= <declaration> <declaration_list> | <declaration>
<declaration> ::= <var_declaration> | <fun_declaration> | <stmt_list>
<var> ::= T_IDENTIFIER
<var_declaration> ::= T_MUT <var> | <var>
<type> ::= T_I32 | T_F32 | T_CHAR | T_BOOL

<fun_declaration> ::= <fun_definition> <compound_stmt>
<fun_definition> ::= T_FN T_IDENTIFIER T_LEFT_PAREN <param_list> T_RIGHT_PAREN | T_FN T_IDENTIFIER T_LEFT_PAREN T_RIGHT_PAREN | T_FN T_IDENTIFIER T_LEFT_PAREN <param_list> T_RIGHT_PAREN T_ARROW <type> | T_FN T_IDENTIFIER T_LEFT_PAREN T_RIGHT_PAREN T_ARROW <type>
<param_list> ::= <param> | <param_list> T_COMMA <param>
<param> ::= <var_declaration> T_COLON <type>

<compound_stmt> ::= T_LEFT_BRACE <stmt_list> T_RIGHT_BRACE | T_LEFT_BRACE T_RIGHT_BRACE
<stmt_list> ::= <stmt> <stmt_list> | <stmt>
<stmt> ::=  T_SEMICOLON | <return_stmt> T_SEMICOLON | <var_declaration_stmt> T_SEMICOLON | <assignment_stmt> T_SEMICOLON | <var_declaration_assignment_stmt> T_SEMICOLON | <expression> T_SEMICOLON | <selection_stmt> | <iteration_stmt> | <break_stmt> T_SEMICOLON | <continue_stmt> T_SEMICOLON

<return_stmt> ::= T_RETURN | T_RETURN <expression>
<var_declaration_stmt> ::= T_LET <var_declaration> T_COLON <type> | T_LET <var_declaration>
<assignment_stmt> ::= <var> T_ASSIGN <expression>
<var_declaration_assignment_stmt> ::= T_LET <var_declaration> T_COLON <type> T_ASSIGN <expression> | T_LET <var_declaration> T_ASSIGN <expression>

<expression> ::= <additive_expression> | <expression> <comparison_operator> <additive_expression>
<additive_expression> ::= <term> | <additive_expression> <additive_operator> <term>
<term> ::= <factor> | <term> <multiplicative_operator> <factor>
<factor> ::= <element>
<element> ::= T_INTEGER_LITERAL | <var> | T_LEFT_PAREN <expression> T_RIGHT_PAREN | <fun_call>

<fun_call> ::= T_IDENTIFIER T_LEFT_PAREN <argument> T_RIGHT_PAREN | T_IDENTIFIER T_LEFT_PAREN T_RIGHT_PAREN
<argument> ::= <expression> | <expression> T_COMMA <argument>

<additive_operator> ::= T_PLUS | T_MINUS
<multiplicative_operator> ::= T_MULTIPLY | T_DIVIDE
<comparison_operator> ::= T_LESS | T_GREATER | T_EQUAL | T_GREATEREQUAL | T_LESSEQUAL | T_NOTEQUAL

<selection_stmt> ::= T_IF <expression> <compound_stmt> | T_IF <expression> <compound_stmt> <else_part>
<else_part> ::= T_ELSE <compound_stmt> | T_ELSE T_IF <expression> <compound_stmt> <else_part>

<iteration_stmt> ::= <while_statement> | <for_statement> | <loop_statement>
<while_statement> ::= T_WHILE <expression> <compound_stmt>
<for_statement> ::= T_FOR <var_declaration> T_IN <iterable_structure> <compound_stmt>
<iterable_structure> ::= <expression> T_DOTDOT <expression>
<loop_statement> ::= T_LOOP <compound_stmt>
<break_stmt> ::= T_BREAK
<continue_stmt> ::= T_CONTINUE
