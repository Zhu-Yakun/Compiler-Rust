<S> 文件结束符
fn 标识符 i32 T_F32 = mut ( ) { } 文件结束符 ; 整数 字符串 字符 浮点数 return , : -> let + - * / < > == >= <= != if else while for loop in .. break continue &

<S> ::= <程序>
<程序> ::= <声明串>
<声明串> ::= <声明> <声明串> | <声明>
<声明> ::= <变量声明内部> | <函数声明> | <语句串>
<可赋值元素> ::= 标识符
<变量声明内部> ::= mut <可赋值元素> | <可赋值元素>
<类型> ::= i32 | T_F32

<函数声明> ::= <函数头声明> <语句块>
<函数头声明> ::= fn 标识符 ( <形参列表> ) | fn 标识符 ( ) | fn 标识符 ( <形参列表> ) -> <类型> | fn 标识符 ( ) -> <类型>
<形参列表> ::= <形参> | <形参列表> , <形参>
<形参> ::= <变量声明内部> : <类型>

<语句块> ::= { <语句串> } | { }
<语句串> ::= <语句> <语句串> | <语句>
<语句> ::=  ; | <返回语句> ; | <变量声明语句> ; | <赋值语句> ; | <变量声明赋值语句> ; | <表达式> ; | <选择语句> | <循环语句> | <跳出语句> ; | <继续语句> ;

<返回语句> ::= return | return <表达式>
<变量声明语句> ::= let <变量声明内部> : <类型> | let <变量声明内部>
<赋值语句> ::= <可赋值元素> = <表达式>
<变量声明赋值语句> ::= let <变量声明内部> : <类型> = <表达式> | let <变量声明内部> = <表达式>

<表达式> ::= <加法表达式> | <表达式> <比较运算符> <加法表达式>
<加法表达式> ::= <项> | <加法表达式> <加减运算符> <项>
<项> ::= <因子> | <项> <乘除运算符> <因子>
<因子> ::= <元素>
<元素> ::= 整数 | <可赋值元素> | ( <表达式> ) | <函数调用>

<函数调用> ::= 标识符 ( <实参列表> ) | 标识符 ( )
<实参列表> ::= <表达式> | <表达式> , <实参列表>

<加减运算符> ::= + | -
<乘除运算符> ::= * | /
<比较运算符> ::= < | > | == | >= | <= | !=

<选择语句> ::= if <表达式> <语句块> | if <表达式> <语句块> <else部分>
<else部分> ::= else <语句块> | else if <表达式> <语句块> <else部分>

<循环语句> ::= <while语句> | <for语句> | <循环语句>
<while语句> ::= while <表达式> <语句块>
<for语句> ::= for <变量声明内部> in <可迭代结构> <语句块>
<可迭代结构> ::= <表达式> .. <表达式>
<循环语句> ::= loop <语句块>
<跳出语句> ::= break
<继续语句> ::= continue

<因子> ::=  * <因子> | & mut <因子>
<类型> :: = & mut <类型> | & <类型>
